apply plugin: 'distribution'

version = '0.2-SNAPSHOT'

def userHome = System.properties['user.home']
def vimHome = "${userHome}/.vim"

task installVimball(type: Copy) {
    group 'vim-gradle'
    description 'Installs Gradle syntax highlighting for Vim as Vimball .'

    from(projectDir) {
        exclude 'build.gradle'
        exclude 'build'
        exclude 'LICENSE.txt'
        exclude 'README.md'
        exclude '.gradle'
        exclude '**/*.swp'
    }
    into vimHome
}

task uninstallVimball(type: Delete) {
    group 'vim-gradle'
    description 'Uninstalls Gradle syntax highlighting Vimball.'

    delete "${vimHome}/after/syntax/groovy.vim",
           "${vimHome}/compiler/gradle.vim",
           "${vimHome}/ctags/gradle.ctags",
           "${vimHome}/ctags/groovy.ctags",
           "${vimHome}/ftdetect/gradle.vim",
           "${vimHome}/ftplugin/tagbar.vim",
           "${vimHome}/syntax/gradle.vim"
}

task installBundle(type: Copy) {
    group 'vim-gradle'
    description 'Installs Gradle syntax highlighting for Vim as bundle.'

    from(projectDir) {
        exclude 'build.gradle'
        exclude 'build'
        exclude '.gradle'
        exclude '**/*.swp'
    }
    into "${vimHome}/bundle/vim-gradle"
}

task uninstallBundle(type: Delete) {
    group 'vim-gradle'
    description 'Uninstalls Gradle syntax highlighting bundle.'

    delete "${vimHome}/bundle/vim-gradle"
}

distributions {
    main {
        contents {
            from(projectDir) {
                exclude 'build'
                exclude '.gradle'
                exclude '**/*.swp'
            }
        }
    }

    vim {
        contents {
            baseName = project.name
            from(projectDir) {
                exclude 'build.gradle'
                exclude 'LICENSE.txt'
                exclude 'README.md'
                exclude 'build'
                exclude '.gradle'
                exclude '**/*.swp'
            }
        }
    }
}
